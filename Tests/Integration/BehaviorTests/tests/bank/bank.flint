contract Bank {
  var manager: Address
  var balances: [Address: Wei] = [:]
  var accounts: [Address] = []
  var lastIndex: Int = 0

  var totalDonations: Wei = Wei(0)
}

Bank :: account <- (any) {
  public init(manager: Address) {
    self.manager = manager
  }

  public func register() mutates (accounts, lastIndex) {
    accounts[lastIndex] = account
    lastIndex += 1
  }

  public func getManager() -> Address {
    return manager
  }

  @payable
  public func donate(implicit value: Wei) mutates (totalDonations, value) {
    totalDonations.transfer(source: &value)
  }
}

Bank :: (manager) {
  public func freeDeposit(account: Address, amount: Int) mutates (balances) {
    var w: Wei = Wei(amount)
    balances[account].transfer(source: &w)
  }

  public func clear(account: Address) mutates (balances) {
    balances[account] = Wei(0)
  }

  public func getDonations() -> Int {
    return totalDonations.getRawValue()
  }
}

Bank :: account <- (accounts) {
  public func getBalance() -> Int {
    return balances[account].getRawValue()
  }

  @payable
  public func deposit(implicit value: Wei) mutates (balances, value) {
    balances[account].transfer(source: &value)
  }

  public func transfer(amount: Int, destination: Address) mutates(balances) {
    balances[destination].transfer(source: &balances[account], amount: amount)
  }

  public func withdraw(amount: Int) mutates(amount) {
    let w: Wei = Wei(&balances[account], amount)
    send(account, &w)
  }
}
