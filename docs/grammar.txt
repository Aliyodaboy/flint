// Top-level declarations

top-level-declarations -> top-level-declaration (top-level-declarations)
top-level-declaration -> contract-declaration | contract-behavior-declaration | struct-declaration

// Contract declaration

contract-declaration -> [contract] identifier [{] (variable-declarations) [}]

// Variable declarations

variable-declarations -> variable-declaration (variable-declarations)
variable-declaration -> [var] identifier type-annotation ([=] expression)

// Type annotations

type-annotation -> [:] type

// Types

type -> identifier (generic-argument-clause) | [[] type []] | type [[] numeric-literal []] | [[] type [:] type []]

generic-argument-clause -> [<] generic-parameter-list [>]
generic-argument-list -> type | type [,] generic-argument-list

// Struct declaration

struct-declaration -> [struct] identifier [{] (struct-members) [}]
struct-members -> struct-member (struct-members)
struct-member -> variable-declaration | function-declaration | initializer-declaration

// Contract behavior declaration

contract-behavior-declaration -> identifier [::] (caller-capability-binding) caller-capability-group (contract-behavior-members)
contract-behavior-members -> contract-behavior-member (contract-behavior-members)
contract-behavior-member -> function-declaration | initializer-declaration

// Caller capability group

caller-capability-group -> [(] caller-capability-list [)]
caller-capability-list -> caller-capability-identifier | caller-capability-identifier [,] caller-capability-list
caller-capability-identifier -> identifier
caller-capability-binding -> identifier [<-]

// Identifier

identifier -> [a-zA-Z] . [a-zA-Z0-9]*

// Function and initializer declarations

initializer-declaration -> initializer-head parameter-clause code-block
function-declaration -> function-head identifier parameter-clause (function-result) code-block

initializer-head -> (declaration-attributes) (declaration-modifiers) [init]
function-head -> (declaration-attributes) (declaration-modifiers) [func]

declaration-modifier -> [public] | [mutating]
declaration-modifiers -> declaration-modifier (declaration-modifiers)

function-result -> [->] type

parameter-clause -> [(] [)] | [(] parameter-list [)]
parameter-list -> parameter | parameter [,] parameter-list
parameter -> identifier type-annotation

declaration-attribute -> [@] . [a-zA-Z]*
declaration-attributes -> declaration-attribute (declaration-attributes)

// Code block

code-block -> [{] statements [}]

// Statements

statements -> statement (statements)
statement -> expression
statement -> [return] (expression)
statement -> if-statement

// Expression

expression -> identifier | in-out-expression | binary-expression | function-call | literal | bracketed-expression | subscript-expression
in-out-expression -> [&] expression
binary-expression -> expression binary-operator expression
bracketed-expression -> [(] expression [)]
subscript-expression -> identifier [[] expression []]

// Function Call

function-call -> identifier function-call-argument-clause
function-call-argument-clause -> [(] [)] | [(] function-call-argument-list [)]
function-call-argument-list -> expression | expression [,] function-call-argument-list

// Binary Operators

binary-operator -> [+] | [-] | [=] | [.]

// Branching

if-statement -> [if] expression code-block (else-clause)
else-clause -> [else] code-block

// Literal

literal -> numeric-literal | string-literal | boolean-literal

numeric-literal -> decimal-literal
decimal-literal -> [0-9]+ | [0-9]+ [.] [0-9]+

array-literal -> [[] array-literal-elements []]
array-literal-elements -> expression ([,] array-literal-elements)
dictionary-literal -> [[] dictionary-literal-elements []]
dictionary-literal-elements -> expression [:] expression ([,] dictionary-literal-elements)

string-literal -> ["] [a-zA-Z0-9]* ["]
boolean-literal -> [true] | [false]
