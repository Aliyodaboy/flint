// SimpleAuction.flint
//
// Allows a user to launch an auction and collect the highest bid.
// When the previous highest bidder is outbid, their bid is refunded
// immediately.

contract SimpleAuction {
  let beneficiary: Address
  var hasAuctionEnded: Bool = false

  var highestBidder: Address
  var highestBid: Wei = Wei(0)

  event highestBidDidIncrease (bidder: Address, amount: Int)
  event auctionDidEnd (winner: Address, bid: Int)
}

SimpleAuction :: caller <- (any) {
  public init()
    mutates(Wei.rawValue)
  {
    beneficiary = caller
    highestBidder = caller
    highestBid = Wei(0)
  }

  @payable
  public func bid(implicit value: Wei)
    mutates (highestBidder, Wei.rawValue)
  {
    if hasAuctionEnded { fatalError() }
    if value.getRawValue() <= highestBid.getRawValue() { fatalError() }

    if highestBid.getRawValue() > 0 {
      // Reimburse the previous highest bidder.
      send(highestBidder, &highestBid)
    }

    // Set the new highest bidder.
    highestBidder = caller

    // Record the new highest bid.
    highestBid.transfer(source: &value)

    let amount: Int = value.getRawValue()
    emit highestBidDidIncrease(bidder: caller, amount: amount)
  }

  public func getHighestBid() -> Int {
    return highestBid.getRawValue()
  }

  public func getHighestBidder() -> Address {
    return highestBidder
  }
}

SimpleAuction :: (beneficiary) {
  public func endAuction()
    mutates (hasAuctionEnded, Wei.rawValue)
  {
    if hasAuctionEnded { fatalError() }

    hasAuctionEnded = true

    send(beneficiary, &highestBid)

    let bid: Int = highestBid.getRawValue()
    emit auctionDidEnd(winner: highestBidder, bid: bid)
  }
}
