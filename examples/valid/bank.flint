contract Bank {
  var manager: Address
  var balances: [Address: Int] = [:]
  var accounts: [Address] = []
  var lastIndex: Int = 0

  var totalDonations: Wei = 0
  var didCompleteTransfer: Event<Address, Address, Int>
}

Bank :: account <- (any) {

  public init(manager: Address) {
    self.manager = manager
  }

  public mutating func register() {
    accounts[lastIndex] = account
    lastIndex += 1
  }

  public func getManager() -> Address {
    return manager
  }

  @payable
  public mutating func donate(implicit value: Wei) {
    totalDonations = totalDonations + value
  }
}

Bank :: (manager) {
  public mutating func freeDeposit(account: Address, amount: Int) {
    balances[account] += amount
  }

  public mutating func clear(account: Int) {
    balances[account] = 0
  }

  public func getDonations() -> Wei {
    return totalDonations
  }
}

Bank :: account <- (accounts) {
  public func getBalance() -> Int {
    return balances[account]
  }

  public mutating func transfer(amount: Int, destination: Address) {
    balances[account] -= amount
    balances[destination] += amount
    didCompleteTransfer(account, destination, amount)
  }
}
